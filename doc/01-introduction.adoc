= Introduction
// TODO update these to real values
:cljdoc-host: https://cljdoc.org
:cljdoc-api-url: {cljdoc-host}/d/lread/rewrite-cljc-playground/CURRENT/api
:toclevels: 5
:toc:

== Introduction
The rewrite-cljc library reads and writes Clojure, ClojureScript and https://github.com/edn-format/edn[EDN] source code while preserving whitespace and comments.


=== Interesting Alternatives
If rewrite-cljc is not your cup of tea, consider following alternatives:

|===
| Project | Parsing? | Writing? | Whitespace Perserving? | Includes Element Row/Col?

| https://github.com/carocad/parcera[parcera]
| yes
| yes
| yes
| yes

| https://github.com/borkdude/edamame[edamame]
| yes
| no
| no
| yes

|===

=== History
https://github.com/xsc[@xsc] created https://github.com/xsc/rewrite-clj[rewrite-clj] in 2013.
Its original use was to upgrade dependencies in https://leiningen.org[lein] project.clj files.
The library quickly took off in popularity and is the default choice for modifying Clojure/EDN from Clojure.

https://github.com/rundis[@rundis] created rewrite-cljs in 2015 to match the features of rewrite-clj for ClojureScript developers.
It was originally used for refactoring support in https://github.com/LightTable/LightTable[Light Table].
In January of 2019, @rundis graciously https://github.com/clj-commons/rewrite-cljs[transferred rewrite-cljs to clj-commons].

Seeing an opportunity to give back to the Clojure community, in 2019 https://github.com/lread[@lread], with guidance and help from many friendly Clojurians, started work on rewrite-cljc.
The goal is to provide a thoughtfully maintained feature-complete library that can be used from either Clojure or ClojureScript.
While merging rewrite-clj and rewrite-cljs to create rewrite-cljc was not trivial, the real hard work was done over many years in rewrite-clj and rewrite-cljs under the leadership of @xsc and @rundis (thanks guys!).
Read link:design/01-merging-rewrite-clj-and-rewrite-cljs.adoc[Merging rewrite-clj and rewrite-cljs] for details on the rewrite-cljc merge.

=== Upgrading from rewrite-clj and/or rewrite-cljs
Both rewrite-clj and rewrite-cljs have a root namespace of `rewrite-clj`.
The rewrite-cljc library has a root namespace of `rewrite-cljc`.

To upgrade to rewrite-cljc, update your project dependencies, and rename your `ns` `:requires` from `rewrite-clj` to `rewrite-cljc` in your source files.

The most notable breaking changes relate to handling of namespaced elements:

* Namespaced map handling was incomplete in rewrite-clj and has been reworked a bit and now supports namespaced symbols
* An `sexpr` on a namespaced key or symbol should now work even when navigating down to the key node
* An `sexpr` that involves auto resolve never consults `\*ns*` you can plugin your own auto-resolve behavior, see link:#namespaced-elements[namespaced elements]

Need to know more? link:design/01-merging-rewrite-clj-and-rewrite-cljs.adoc#_changes[Details of changes].

[#nodes]
=== Rewrite-cljc Nodes

Rewrite-cljc parses Clojure source into rewrite-cljc nodes.

While reviewing the following example, it helps to remember that Clojure source is simply data.

image::introduction-parsed-nodes.png[introduction parsed nodes]

You can work with these link:#parser-api[nodes directly] or take advantage of the power of the link:#zip-api[zip API].

Rewrite-cljc offers easy conversion from rewrite-cljc nodes to Clojure forms and back.
This is usually very convenient but does come with some caveats.
As you get more experienced with rewrite-cljc, you will want to review link:#sexpr-nuances[sexpr nuances].

=== Conventions
As is the common convention, example code shows results of expression evaluation like so:

[source, clojure]
----
(+ 1 2 3)
;; => 6
----

We show distinctions between Clojure and Clojuescript results like so:
[source, clojure]
----
\c
;; =clj=> \c
;; =cljs=> "c"
----

And we show output like so:
[source, clojure]
----
(println "hello there")
;; =stdout=>
;; hello there
----

=== Terminology

Rewrite-cljc has an `sexpr` function that is described as returning Clojure forms.
Its usage of the terms "s-expression" and "forms" might be less nuanced than some formal definitions.
I think we are in line with https://www.braveclojure.com/do-things/#Forms[Clojure for the Brave and True's description of forms].
To us a Clojure form is any parsed (but not evaluated) Clojure as it would be returned by the Clojure reader.

Within reason, Clojure's `read-string` and rewrite-cljc's `sexpr` functions should return equivalent Clojure forms.
To illustrate, some code:

[source, clojure]
----
(require '[rewrite-cljc.zip :as z]
         '[rewrite-clj.parser :as p]
         '[rewrite-cljc.node :as n])

(defn form-test [s]
  (let [forms [(-> s read-string)
               (-> s z/of-string z/sexpr)
               (-> s p/parse-string n/sexpr)]]
    (if (apply = forms)
      (first forms)
      [:not-equal forms])))

(form-test "a")
;; => a
(form-test "[1 2 3]")
;; => [1 2 3]
(form-test "(defn hello [name] (println \"Hello\" name))")
;; => (defn hello [name] (println "Hello" name))
----

== Rewrite-cljc APIs
There are 4 public API namespaces:

. `rewrite-cljc.zip`
. `rewrite-cljc.parser`
. `rewrite-cljc.node`
. `rewrite-cljc.paredit`

[#zip-api]
=== Zip API
Traverse and modify Clojure/ClojureScript/EDN.
This is considered the main rewrite-cljc API and might very well be all you need.

You'll optionally use the link:#node-api[node API] on the returned nodes.

==== A Brief Introduction to Zippers

[TIP]
====
Rewrite-cljc uses a customized version of
https://clojure.github.io/clojure/clojure.zip-api.html[Clojure's clojure.zip].
If you are not familiar with zippers, you may find the following resources helpful:

* https://clojure.org/reference/other_libraries#_zippers_functional_tree_editing_clojure_zip[Clojure overview of zippers]
* https://lambdaisland.com/blog/2018-11-26-art-tree-shaping-clojure-zip[Arne Brasseur - The Art of Tree Shaping with Clojure Zippers]
* https://tbaldridge.pivotshare.com/media/zippers-episode-1/11348/feature?t=0[Tim Baldrige - PivotShare - Series of 7 Videos on Clojure Zippers]
====

At a conceptual level, the rewrite-cljc zipper holds:

* a tree of rewrite-cljc nodes representing your parsed Clojure source
* your current location within the zipper

Because the zipper holds both the tree and your location within the tree, its variable is commonly named `zloc`.
The zipper is immutable, as such, location changes and node modifications are always returned in a new zipper.

You may want to refer to link:#nodes[Rewrite-cljc Nodes] while reviewing this introductory example:

[source, clojure]
----
(require '[rewrite-cljc.zip :as z])

;; define some test data
(def data-string
"(defn my-function [a]
  ;; a comment
  (* a 3))")

;; parse code to nodes, create a zipper, and navigate to the first non-whitespace node
(def zloc (z/of-string data-string))

;; explore what we've parsed
(z/sexpr zloc)
;; => (defn my-function [a] (* a 3))
(-> zloc z/down z/right z/node)
;; => <token: my-function>
(-> zloc z/down z/right z/sexpr)
;; => my-function

;; rename my-function to my-function2 and return resulting s-expression
(- zloc
    z/down
    z/right
    (z/edit (comp symbol str) "2")
    z/up
    z/sexpr)
;; => (defn my-function2 [a] (* a 3))

;; rename my-function to my-function2 and return updated string from root node
(-> zloc
    z/down
    z/right
    (z/edit (comp symbol str) "2")
    z/root-string
    println)
;; =stdout=>
;; (defn my-function2 [a]
;;  ;; a comment
;;  (* a 3))

----

TIP: The zip location movement functions (`right`, `left`, `up`, `down`, etc) skip over whitespace nodes.
If you want to see all nodes, use the `+*+` counterparts (`right*`, `left*`, `up*`, `down*`, etc).

See link:{cljdoc-api-url}/rewrite-cljc.zip[zip API docs].

==== Finding Elements with the Zip API

The `rewrite-cljc.zip` namespace includes find operations to navigate to locations of interest in your zipper.
Let's assume you want to modify the following minimal `project.clj` by replacing the `:description` placeholder text with something more meaningful:

.project.clj snippet
[source, clojure]
----
(defproject my-project "0.1.0-SNAPSHOT"
  :description "Enter description")
----

Most find functions accept an optional location movement function.
Use:

* `rewrite-cljc.zip/right` (the default) - to search sibling nodes to the right
* `rewrite-cljc.zip/left` to search siblings to left
* `rewrite-cljc.zip/next` for a depth-first tree search

[source,clojure]
----
(require '[rewrite-cljc.zip :as z])

;; parse and create a zipper from the project.clj file
(def zloc (z/of-file "project.clj")) ;; <1>
;; for convenience, here's the same but from a string
(def zloc (z/of-string
"(defproject my-project \"0.1.0-SNAPSHOT\"
  :description \"Enter description\")"))

;; find defproject by navigating depth-first
(def zloc-defproject (z/find-value zloc z/next 'defproject))
;; verify that we are where we think we are
(z/sexpr zloc-defproject)
;; => defproject

;; search right for :description and then move one node to the right ;; <2>
(def zloc-desc (-> zloc-defproject (z/find-value :description) z/right))
;; check that this worked
(z/sexpr zloc-desc)
;; => "Enter description"

;; replace node at current location and return the result
(-> zloc-desc (z/replace "My first Project.") z/root-string println)
;; =stdout=>
;; (defproject my-project "0.1.0-SNAPSHOT"
;;   :description "My first Project.")
----
<1> reading from a file is only available from Clojure
<2> Remember that while whitespace is preserved, it is automatically skipped during navigation.

==== Familiar Functions for Updating Nodes with the Zip API

The zip API provides familiar ways to work with parsed Clojure data structures.
It offers some functions that correspond to the standard seq functions, for example:

[source, clojure]
----
(require '[rewrite-cljc.zip :as z])

(def zloc (z/of-string "[1\n2\n3]"))
(z/vector? zloc)
;; => true
(z/sexpr zloc)
;; => [1 2 3]
(-> zloc (z/get 1) z/node)
;; => <token: 2>
(-> zloc (z/assoc 1 5) z/sexpr)
;; => [1 5 3]
(->> zloc (z/map #(z/edit % + 4)) z/root-string)
;; => "[5\n6\n7]"

(def zloc (z/of-string "{:a 10 :b 20}"))
(z/map? zloc)
;; => true
(-> zloc (z/get :b) z/node)
;; => <token: 20>
(-> zloc (z/assoc :b 42) z/sexpr)
;; => {:b 42, :a 10}
(->> zloc (z/map-vals #(z/edit % inc)) z/root-string)
;; => "{:a 11 :b 21}"
(->> zloc
     (z/map-keys #(z/edit %
                          (fn [v] (keyword "prefix" (name v))) ))
     z/root-string)
;; => "{:prefix/a 10 :prefix/b 20}"
----

==== Tracking Position with the Zip API

If you need to track the source row and column while reading and manipulating your zipper, create your zipper with `:track-position true` option.
Note that the row and column are 1-based.

[source,clojure]
----
(require '[rewrite-cljc.zip :as z])

;; parse some Clojure into a position tracking zipper
(def zloc (z/of-string
           "(defn sum-me\n  \"Add 'em up!\"\n  [a b c]\n  (+ a\n     c))"
           {:track-position? true}))

;; let's see what that looks like printed out
(println (z/root-string zloc))
;; =stdout=>
;; (defn sum-me
;;   "Add 'em up!"
;;   [a b c]
;;   (+ a
;;      c))

;; navigate to second z in zipper
(def zloc-c (-> zloc
            (z/find-value z/next '+)
            (z/find-value z/next 'c))) ;; <3>

;; check if current node is as expected
(z/string zloc-c)
;; => "c"

;; examine position of second z, it is on 6th column of the 5th row
(z/position zloc-c)
;; => [5 6]

;; insert new element b with indentation and alignment
(def zloc-c2 (-> zloc-c
                 (z/insert-left 'b)
                 (z/insert-newline-left)
                 (z/insert-space-left 5)))

;; we should still be at c
(z/string zloc-c2)
"c"

;; output our updated Clojure
(println (z/root-string zloc-c2))
;; =stdout=>
;; (defn sum-me
;;   "Add 'em up!"
;;   [a b c]
;;   (+ a
;;      b
;;      c))

;; and check that location of c has been updated, it should now be on the 6th column of the 6th row
(z/position zloc-c2)
;; => [6 6]
----

[#parser-api]
=== Parser API
Parses Clojure/ClojureScript/EDN to rewrite-cljc nodes.
The link:#zip-api[zip API] makes use of the parser API to parse Clojure into zippers.
If your focus is parsing instead of rewriting, you might find this lower level API useful.
You can choose to parse one or all forms from a string or a file.footnote:file[]

The parser API takes advantage of https://github.com/clojure/tools.reader[clojure.tools.reader] for simple forms.

Here we parse a single form from a string:

[source, clojure]
----
(require '[rewrite-cljc.parser :as p])

(def form-nodes (p/parse-string "(defn my-function [a]\n  (* a 3))"))

form-nodes
;; => <list:
  (defn my-function [a]
    (* a 3))
>
----

You'll likely use the link:#node-api[node API] on the returned nodes.

See link:{cljdoc-api-url}/rewrite-cljc.parser[parser API docs].

[#node-api]
=== Node API
Inspect, analyze and create rewrite-cljc nodes.

[source, clojure]
----
(require '[rewrite-cljc.parser :as p]
         '[rewrite-cljc.node :as n])

(def nodes (p/parse-string "(defn my-function [a]\n  (* a 3))"))

;; Explore what we've parsed
(n/tag nodes)
;; => :list

(n/children nodes)
;; => (<token: defn> <whitespace: " "> <token: my-function> <whitespace: " "> <vector: [a]> <newline: "\n"> <whitespace: "  "> <list: (* a 3)>)

(n/sexpr nodes)
;; => (defn my-function [a] (* a 3))

(n/child-sexprs nodes)
;; => (defn my-function [a] (* a 3))

;; convert the nodes back to a printable string
(n/string nodes)
;; => "(defn my-function [a]\n  (* a 3))"

;; coerce clojure forms to rewrite-cljc nodes
(n/coerce '[a b c])
;; => <vector: [a b c]>

;; create rewrite-cljc nodes by hand
(n/meta-node
 (n/token-node :private)
 (n/token-node 'sym))
;; => <meta: ^:private sym>
----

See link:{cljdoc-api-url}/rewrite-cljc.node[node API docs].

=== Paredit API
Structured editing was introduce by rewrite-cljs and carried over to rewrite-cljc.

We might expand this section if there is interest, but the docstrings should get you started.

See link:{cljdoc-api-url}/rewrite-cljc.paredit[current paredit API docs].


[#sexpr-nuances]
== Sexpr Nuances

Rewrite-cljc parses arbitrary Clojure/ClojureScript source code into rewrite-cljc nodes.
Converting rewrite-cljc nodes to Clojure forms via `sexpr` is convenient, but it does come with some caveats.

=== Whitespace
The whitespace that a rewrite-cljc so carefully preserves is lost when converting to a Clojure form.

[source,clojure]
----
(require '[rewrite-cljc.parser :as p]
         '[rewrite-cljc.node :as n])

;; parse some Clojure source
(def nodes (p/parse-string "{  :a 1\n\n   :b 2}"))

;; print it out to show the whitespace
(println (n/string nodes))
;; =stdout=>
;; {  :a 1
;;
;;    :b 2}

;; print out Clojure forms and notice the loss of the specifics of whitespace and element ordering
(pr (n/sexpr nodes))
;; =stdout=>
;; {:b 2, :a 1}
----

=== Not all Source is Sexpr-able

Some source code elements are not sexpr-able.
Reader ignore/discard `#_`, comment and whitespace all throw an "unsupported operation" exception.

[source, clojure]
----
(require '[rewrite-cljc.zip :as z])

(-> (z/of-string "#_42") z/sexpr)
(-> (z/of-string ";; canâ€™t sexpr me!") z/next* z/sexpr) ;; <1>
(-> (z/of-string " ") z/next* z/sexpr) ;; <1>
----
<1> Notice the use of `next*` to include normally skipped nodes.

=== Differences in Clojure Platforms

Clojure and ClojureScript have differences, some examples you might run into when using `sexpr` are:

[source, clojure]
----
(require '[rewrite-cljc.zip :as z])

;; ClojureScript has no Ratio type
(-> (z/of-string "3/4") z/sexpr)
;; =clj=> 3/4
;; =cljs=> 0.75

;; Integral type and behaviour is defined by host platforms
(+ 10 (z/of-string "9007199254740991") z/sexpr)
;; =clj=> 9007199254741001
;; =cljs=> 9007199254741000

;; ClojureScript has not character type, characters are expressed as strings
(-> (z/of-string "\\a") z/sexpr)
;; =clj=> \a
;; =cljs=> "a"
----

Note that these differences affect `sexpr` only.
Rewrite-cljc should be able to parse and rewrite all valid Clojure/ClojureScript code.

[#namespaced-elements]
=== Namespaced Elements

==== Recap
In Clojure keywords and symbols can be qualified.
A recap via examples:

* Stand-alone keyword and symbols:
+
|===
| |keyword|symbol

|unqualified
a|`:my-kw`
a|`'my-symbol`

|qualified
a|`:prefix/my-kw`
a|`'prefix/my-symbol`

|auto-resolved current namespace
a|`::my-kw`
a|n/a

|auto-resolved namespaced alias
a|`::my-ns-alias/my-kw`
a|n/a

|===

* Namespaced keyword and symbols:
+
|===
| |keyword|symbol

|unqualified
a|`#:prefix{:_/my-kw 1}`
a|`'#:prefix{_/my-symbol =}`

|qualified
a|`#:prefix{:my-kw 1}`
a|`'#:prefix{my-symbol 1}`

|auto-resolved current namespace
a|`#::{:my-kw 1}`
a|`'#::{my-symbol 1}`

|auto-resolved namespaced alias
a|`#::my-ns-alias{:my-kw 1}`
a|`'#::my-ns-alias{my-symbol 1}`

|===

==== Rewrite-cljc Default Handling

When calling `sepxr` on auto-resolved keyword or symbol node, rewrite-cljc will resolve:

* the current ns to `user`
* namespace alias `x` to `x-unresolved`

[source, clojure]
----
(require '[rewrite-cljc.parser :as p]
         '[rewrite-cljc.node :as n])

(-> (p/parse-string "::kw") n/sexpr)
;; => :user/kw
(-> (p/parse-string "#::{:a 1 :b 2 s1 3}") n/sexpr)
;; => #:user{s1 3, :b 2, :a 1}
(-> (p/parse-string "::my-alias/kw") n/sexpr)
;; => :my-alias-unresolved/kw
(-> (p/parse-string "#::my-alias{:a 1 :b 2 s1 3}") n/sexpr)
;; => #:my-alias-unresolved{s1 3, :b 2, :a 1}
----

This default behavior should be sufficient for most use cases (let us know if we are wrong about that).
TODO: are these really useful defaults?

==== Custom Auto-Resolve Handling

If you'd prefer a different behavior, you can specify your own auto-resolve function.

The `:auto-resolve` function takes a single arg `alias` for lookup and must return symbol.
The `alias` will be:

* `:current` for a request for the current namespace
* otherwise it will be a symbol for the namespace alias to lookup

For example, if you know the current namespace and the namespace aliases it loads, you can specify them:
[source, clojure]
----
(require '[rewrite-cljc.parser :as p]
         '[rewrite-cljc.node :as n])

(defn resolver [alias]
  (or (get {:current 'my.current.ns
            'my-alias 'my.aliased.ns} alias)
      (symbol (str alias "-unresolved"))))

(-> (p/parse-string "::kw") (n/sexpr {:auto-resolve resolver}))
;; => :my.current.ns/kw
(-> (p/parse-string "#::{:a 1 :b 2 s1 3}") (n/sexpr {:auto-resolve resolver}))
;; => #:my.current.ns{s1 3, :b 2, :a 1}
(-> (p/parse-string "::my-alias/kw") (n/sexpr {:auto-resolve resolver}))
;; => :my.aliased.ns/kw
(-> (p/parse-string "#::my-alias{:a 1 :b 2 s1 3}") (n/sexpr {:auto-resolve resolver}))
;; => #:my.aliased.ns{s1 3, :b 2, :a 1}
----

The `:auto-resolve` option is accepted in the `opts` map arg for:

* The `rewrite-cljc.node` namespace functions `sexpr` and `child-sexpr`.
* The `rewrite-cljc.zip` namespace zipper creation functions `edn*`, `edn`, `of-string` and `of-file`.
The resulting zipper will then automatically apply your auto-resolve within any zip operation that makes use of sexpr. These are:
** `sexpr`
** `find-value` and `find-next-value` - sexpr is applied to each node to get the "value" for comparison
** `edit` - the current node is sexpr-ed
** `get` and `assoc` - sexpr is applied to the map key

==== Impact of Auto-Resolve

Let's illustrate how functions that use sexpr internally are affected by exploring `rewrite-cljc.zip/get`:

[source,clojure]
----
(require '[rewrite-cljc.zip :as z])

;; get on unqualified keys is straightforward:
(-> "{:a 1 :b 2 c 3}" z/of-string (z/get :b) z/node)
;; => <token: 2>

;; get on qualified keys is also easy to grok
(-> "{:a 1 :prefix/b 2 c 3}" z/of-string (z/get :prefix/b) z/node)
;; => <token: 2>
(-> "#:prefix{:a 1 :b 2 c 3}" z/of-string (z/get :prefix/b) z/node)
;; => <token: 2>
(-> "#:prefix{:a 1 :b 2 c 3}" z/of-string (z/get 'prefix/c) z/node)
;; => <token: 3>

;; but when we introduce auto-resolved elements, the default resolver comes into play
;; and must be considered
(-> "{::ns-alias/a 1 ::b 2 c 3}" z/of-string (z/get :user/b) z/node)
;; => <token: 2>
(-> "{::ns-alias/a 1 ::b 2 c 3}" z/of-string (z/get :ns-alias-unresolved/a) z/node)
;; => <token: 1>
(-> "#::{:a 1 :b 2 c 3}" z/of-string (z/get :user/b) z/node)
;; => <token: 2>
(-> "#::{:a 1 :b 2 c 3}" z/of-string (z/get 'user/c) z/node)
;; => <token: 3>
----

==== Impact of Namespaced Map Context on Sexpr

Namespaced map context is automatically applied to symbols and keywords in the namespaced map.

To illustrate:

[source,clojure]
----
(require '[rewrite-cljc.zip :as z])

(def zloc (z/of-string "#:my-prefix {:a 1 :b 2 c 3}"))

;; The Clojure form for the namespaced looks good
( -> zloc z/sexpr)
;; => #:my-prefix{:b 2, c 3, :a 1}

;; To the correct sexpr when navigating down to a map key,
(-> zloc z/down z/rightmost z/down z/sexpr)
;; => :my-prefix/a

;; the key actually holds the namespaced map context
(-> zloc z/down z/rightmost z/down
    z/node
    :map-qualifier
    (select-keys [:auto-resolved? :prefix]))
;; => {:auto-resolved? false, :prefix "my-prefix"}
----

TODO: More thought is required here.

The namespaced map context is automatically reapplied when map children are replaced, but only for namespaced maps.

A node with namespaced map context can be added, updated or moved.

* perhaps there is an `apply-context` that walks the tree and
** removes map context for symbols and keywords that are not in a namespaced map
** updates map context for symbols and keywords that are in a namespaced map

Or do we update context as we are navigating the tree? Maybe z/up reapplies nsmap context?
But we must not forget about users who are working with nodes directly and not using zippers.

== Dealing with Reader Generated Metadata
TODO: maybe hardcode? Either that or move from dynamic var to passed opt.

Rewrite-cljc offers, where it can, transparent coercion from Clojure forms to rewrite-cljc nodes.

Clojure will, in some cases, add location metadata that is not in the original source code, as illustrated here:

.REPL session
[source,clojure]
----
(meta '(1 2 3))
;; => {:line 1, :column 8}
----

Rewrite-cljc will, on coercion from Clojure forms to rewrite-cljc nodes, omit location metadata.
No rewrite-cljc metadata node will will be created if resulting metadata is empty.

On conversion from rewrite-cljc nodes to Clojure forms via `sexpr`, I don't see a way to omit the location metadata.
With the assumption that you will generally coerce Clojure forms back to rewrite-cljc nodes, this should not cause an issue.

You can use `rewrite-cljc.node/form-meta` in place of `clojure.core/meta` if you'd like to only see metadata that was actually in original Clojure source code.

To support those using rewrite-cljc under sci, in addition to `:line` and `:column` rewrite-cljc also removes `:end-line` and `:end-column` metadata.
Note that while Clojure only adds location metadata to quoted lists, sci adds it to all forms that accept metadata.

Omitting location metadata is a deviation from rewrite-clj behavior.
Should you wish, for whatever reason, to preserve rewrite-clj behavior and not remove location metadata, bind `rewrite-cljc.node/\*elide-metadata*` to `nil`.
